plugins {
	id "com.modrinth.minotaur" version '2.+'
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = archives_base_name
version = "$mod_version+$minecraft_version"
group = maven_group

repositories {
	maven {
		name 'legacy-fabric'
		url 'https://maven.legacyfabric.net'
	}
	maven {
		name 'Jitpack'
		url 'https://jitpack.io'
	}
}

loom {
	setIntermediaryUrl(
		'https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar'
	)
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.legacyfabric:yarn:$minecraft_version+build.$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"
	modImplementation "net.legacyfabric.legacy-fabric-api:legacy-fabric-api:$fabric_version+$minecraft_version"

	implementation "com.google.guava:guava:$guava_version"
}

processResources {
	inputs.property 'version', version
	inputs.property 'minecraft_version', minecraft_version

	filesMatching('fabric.mod.json') {
		expand([
			'version': version,
			'minecraft_version': minecraft_version,
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.sourceCompatibility = 8
	it.options.release.set(8)
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from 'LICENSE'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		if ( project.hasProperty('legacy_fabric_maven_publish_token') ) {
			maven {
				url 'https://maven.legacyfabric.net/'
				credentials {
					username 'BobTheBuildSlave'
					password project.getProperty('legacy_fabric_maven_publish_token')
				}
			}
		} else {
			maven {
				maven {
					name 'Repsy'
					credentials(PasswordCredentials)
					url 'https://repsy.io/mvn/enderzombi102/mc'
				}
			}
		}
	}
}

modrinth {
	token = hasProperty('modrinth_token') ? modrinth_token : ''
	projectId = 'XzTYkVLx'
	versionNumber = version // You don't need to set this manually. Will fail if Modrinth has this version already
	versionName = "Legacy Mod Menu $version"
	versionType = 'release' // This is the default
	uploadFile = remapJar // With Fabric Loom or Architectury Loom, this MUST be set to `remapJar` instead of `jar`!
	gameVersions = [ minecraft_version ] // Must be an array, even with only one version
	loaders = [ 'fabric' ] // Must also be an array - no need to specify this if you're using Fabric Loom or ForgeGradle
	dependencies = [ ]
}
